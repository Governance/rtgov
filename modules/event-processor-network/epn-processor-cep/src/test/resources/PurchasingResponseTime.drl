import org.savara.bam.activity.model.soa.RequestReceived
import org.savara.bam.activity.model.soa.ResponseSent

global org.savara.bam.epn.cep.EPNContext epn

declare RequestReceived
    @role( event )
    @timestamp( timestamp )
    @expires( 2m20s )
end

declare ResponseSent
    @role( event )
    @timestamp( timestamp )
    @expires( 2m20s )
end

rule "correlate request and response"
when
    $req : RequestReceived( $id : messageId ) from entry-point "Purchasing" 
    $resp : ResponseSent( replyToId == $id, this after[0,2m20s] $req )  from entry-point "Purchasing"
then

	epn.logInfo("REQUEST: "+$req+" WITH RESPONSE: "+$resp);

	java.util.Properties props=new java.util.Properties();
	props.put("requestId", $req.getMessageId());
	props.put("responseId", $resp.getMessageId());

	long responseTime=$resp.getTimestamp()-$req.getTimestamp();
	
	epn.logDebug("CORRELATION on id '"+$id+"' response time "+responseTime);
	
	props.put("responseTime", responseTime);
	
    epn.forward(props);

end
